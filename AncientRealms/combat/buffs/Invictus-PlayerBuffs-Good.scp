[ITEMDEF i_invictus_playerbuff_reactarmordelay]
NAME=Reactive Armor Delay
ID=8326
TYPE=t_eq_script
LAYER=layer_special
	
ON=@CREATE
	TIMER=10
		
ON=@TIMER	
	REMOVE
	RETURN 1
	
[ITEMDEF i_invictus_playerbuff_reactarmor]
DEFNAME=i_invictus_playerbuff_reactarmor
ID=8326
TYPE=t_eq_script
NAME=Buff ReactAr
LAYER=layer_special

ON=@CREATE
	ATTR=ATTR_NEWBIE|ATTR_DECAY|ATTR_MOVE_NEVER
	MORE1=0
	TIMERD=1

ON=@TIMER
	IF !(<CONT.TAG0.ReactiveArmor>)
		FOR X 1 24
			IF <CONT.FINDLAYER.<LOCAL.X>.TYPE>=t_armor
				CONT.TAG.ArmorColor<EVAL <LOCAL.X>>=<SERV.UID.<CONT.FINDLAYER.<LOCAL.X>.UID>.COLOR>
				CONT.FINDLAYER.<LOCAL.X>.TAG.OriginalColor=<SERV.UID.<CONT.FINDLAYER.<LOCAL.X>.UID>.COLOR>
			ENDIF
			CONT.TAG.ReactiveArmor=1
		ENDFOR
		CONT.SYSMESSAGE @55,,1 Reactive Armour activated
	ENDIF	
	IF (<MORE1> > 5)
		FOR X 1 24
			IF <CONT.FINDLAYER.<LOCAL.X>.TYPE>=t_armor
				SERV.UID.<CONT.FINDLAYER.<LOCAL.X>.UID>.COLOR=<CONT.TAG.ArmorColor<EVAL <LOCAL.X>>>
				CONT.TAG.ArmorColor<EVAL <LOCAL.X>>=
				CONT.TAG.ReactiveArmor=
			ENDIF
		ENDFOR
		CONT.SYSMESSAGE @55,,1 Your Reactive Armour wears off
		CONT.TAG.ReactiveArmor=
		REMOVE
		RETURN 1
	ELSE
		FOR X 1 24
			IF <CONT.FINDLAYER.<LOCAL.X>.TYPE>=t_armor
				SERV.UID.<CONT.FINDLAYER.<LOCAL.X>.UID>.COLOR=color_o_orangeburst
			ENDIF
		ENDFOR
		CONT.EFFECT=3,i_fx_bee_swarm2,64,16,1,00029,3
	ENDIF	
	MORE1=<MORE1>+1
	TIMER=1
	RETURN 1

[ITEMDEF i_invictus_playerbuff_agilitydelay]
NAME=Agility Delay
ID=8328
TYPE=t_eq_script
LAYER=layer_special
	
ON=@CREATE
	TIMER=30
		
ON=@TIMER	
	REMOVE
	RETURN 1
	
[ITEMDEF i_invictus_playerbuff_agility]
DEFNAME=i_invictus_playerbuff_agility
ID=8328
TYPE=t_eq_script
NAME=Buff Agilty
LAYER=layer_special

ON=@CREATE
	ATTR=ATTR_NEWBIE|ATTR_DECAY|ATTR_MOVE_NEVER
	MORE1=0
	TIMERD=1

ON=@TIMER
	MORE1=<EVAL <MORE1>+1>
	IF (<EVAL <MORE1>> > 6)
		CONT.SPEEDMODE=0
		CONT.SYSMESSAGE @2524,,1 You slow down feeling a little refreshed
		REMOVE
	ENDIF
	CONT.EFFECT=3,i_fx_sparkle,64,16,1,00029,3
	IF (<CONT.SPEEDMODE> = 0)
		CONT.SPEEDMODE=1
		CONT.SYSMESSAGE @2524,,1 You feel a sudden burst of energy.
	ELSE
		IF <CONT.STAMINA> < <CONT.DEX>
			CONT.STAMINA=<EVAL <CONT.STAMINA>+1>
		ENDIF
	ENDIF
	TIMER=1
	RETURN 1	

[ITEMDEF i_invictus_playerbuff_strengthdelay]
NAME=Strength Delay
ID=8335
TYPE=t_eq_script
LAYER=layer_special
	
ON=@CREATE
	TIMER=12
		
ON=@TIMER	
	REMOVE
	RETURN 1
	
[ITEMDEF i_invictus_playerbuff_strength]
DEFNAME=i_invictus_playerbuff_strength
ID=8335
TYPE=t_eq_script
NAME=Buff Strength
LAYER=layer_special

ON=@CREATE
	ATTR=ATTR_NEWBIE|ATTR_DECAY|ATTR_MOVE_NEVER
	MORE1=0
	TIMERD=1

ON=@TIMER
	MORE1=<EVAL <MORE1>+1>
	CONT.MODMAXWEIGHT=1000
	CONT.STAMINA=999
	CONT.EFFECT=3,i_fx_bless_effect,64,16,1,92,3
	IF (<MORE1> > 5)
		CONT.STAMINA=<CONT.DEX>
		CONT.MODMAXWEIGHT=0
		CONT.SYSMESSAGE @92,,1 gravity seems to be restored
		REMOVE
		RETURN 1
	ENDIF
	TIMER=1
	RETURN 1

[ITEMDEF i_invictus_playerbuff_cunning]
DEFNAME=i_invictus_playerbuff_cunning
ID=8329
TYPE=t_eq_script
NAME=Buff Cunning
LAYER=layer_special

ON=@CREATE
	ATTR=ATTR_NEWBIE|ATTR_DECAY|ATTR_MOVE_NEVER
	MORE1=0
	TIMER=3

ON=@TIMER
	MORE1=<MORE1>+1
	IF (<MORE1> > 6)
		REMOVE
	ENDIF	
	CONT.EFFECT=3,i_fx_sparkle_2,0,32,0
	FOR X 0 3
		IF (<CONT.MANA> < <CONT.INT>)
			CONT.MANA=<CONT.MANA>+2
		ENDIF
		IF (<CONT.LUCK> < 0)
			CONT.LUCK=0
		ENDIF
	ENDFOR		
	TIMER=3
	RETURN 1

[ITEMDEF i_invictus_playerbuff_bless]
DEFNAME=i_invictus_playerbuff_bless
ID=8336
TYPE=t_eq_script
NAME=Buff Bless
LAYER=layer_special

ON=@CREATE
	ATTR=ATTR_NEWBIE|ATTR_DECAY|ATTR_MOVE_NEVER
	MORE1=0
	TIMERD=1

ON=@TIMER
	MORE1=<MORE1>+1
	IF (<MORE1> > 6)
		REMOVE
	ENDIF	
	CONT.EFFECT=3,i_fx_sparkle_2,0,32,0
	IF (<CONT.HITS> < <CONT.STR>)
		CONT.HITS=<CONT.HITS>+1
	ENDIF
	FOR X 0 3
		IF (<CONT.STAM> < <CONT.DEX>)
			CONT.STAM=<CONT.STAM>+1
		ENDIF
		IF (<CONT.MANA> < <CONT.INT>)
			CONT.MANA=<CONT.MANA>+1
		ENDIF
		IF (<CONT.LUCK> < 0)
			CONT.LUCK=0
		ENDIF
	ENDFOR		
	TIMER=3
	RETURN 1

[ITEMDEF i_invictus_playerbuff_protection]
DEFNAME=i_invictus_playerbuff_protection
ID=8334
TYPE=t_eq_script
NAME=Buff Bless
LAYER=layer_special

ON=@CREATE
	ATTR=ATTR_NEWBIE|ATTR_DECAY|ATTR_MOVE_NEVER
	MORE1=5
	TIMER=5

ON=@TIMER	
	IF (<MORE1>==0)
		CONT.SYSMESSAGE @55 Your Protection wears off
		REMOVE
		RETURN 1
	ENDIF	
	MORE1=<MORE1>+-1
	TIMER=1
	RETURN 1

[ITEMDEF i_invictus_playerbuff_protectiondelay]
NAME=Protection Delay
ID=8334
TYPE=t_eq_script
LAYER=layer_special
	
ON=@CREATE
	TIMER=30
		
ON=@TIMER	
	REMOVE
	RETURN 1

[ITEMDEF i_invictus_playerbuff_reflection]
DEFNAME=i_invictus_playerbuff_reflection
ID=8334
TYPE=t_eq_script
NAME=Buff Reflect
LAYER=layer_special

ON=@CREATE
	ATTR=ATTR_NEWBIE|ATTR_DECAY|ATTR_MOVE_NEVER
	MORE1=8
	TIMER=5

ON=@TIMER
	IF (<MORE1>==0)
		CONT.SYSMESSAGE @55 Your Reflection wears off
		REMOVE
		CONT.TAG.CannotDamageArmour=
		RETURN 1
	ENDIF
	CONT.TAG.CannotDamageArmour=1
	MORE1=<MORE1>+-1
	TIMER=1
	RETURN 1
	
[EOF]